use intern::{self, InternedString};
use repr::*;

grammar;

pub Func: Func = {
    <decls:VarDecl+> <blocks:BasicBlockData+> => Func {
        decls: decls,
        data: blocks.into_iter()
                    .map(|block| (block.name, block))
                    .collect(),
    }
};

VarDecl: VarDecl = {
    Comment* "let" <n:Variable> ":" <t:Ty> ";" => VarDecl {
        name: n,
        ty: t,
    }
};

Ty: Ty<()> = {
    <n:Ident> <a:TyArgs> => Ty { name: n, args: a },
};

TyArgs: Vec<TyArg<()>> = {
    "<" <Comma0<TyArg>> ">",
    () => vec![]
};

TyArg: TyArg<()> = {
    "'" => TyArg::Region(()),
    <t:Ty> => TyArg::Ty(t),
};

BasicBlockData: BasicBlockData = {
    Comment* "block" <name:BasicBlock> "{"
        <actions:Action*>
        <gotos:("goto" <BasicBlock*> ";")?>
    "}" => {
        BasicBlockData {
            name: name,
            actions: actions,
            successors: gotos.unwrap_or(vec![]),
        }
    }
};

Action: Action = {
    <a:Variable> "=" "&" ";" Comment? => Action::Assign(a),
    "use" "(" <v:Variable> ")" ";" Comment? => Action::Use(v),
    "assert" <a:Assertion> ";" Comment? => Action::Assert(a),
    ";" Comment? => Action::Noop,
};

Variable: Variable = {
    <n:Ident> => Variable { name: n }
};

Assertion: Assertion = {
    "in" "(" <v:Variable> ")" => Assertion::In(v),
    "out" "(" <v:Variable> ")" => Assertion::Out(v),
};

BasicBlock: BasicBlock = {
    Ident => BasicBlock(<>)
};

Ident: InternedString = {
    r"[a-zA-Z_]\w*" => intern::intern(<>)
};

Comma<T>: Vec<T> = {
    <Comma1<T>> ","?
};

Comma0<T>: Vec<T> = {
    <v:Comma<T>?> => v.unwrap_or(vec![])
};

Comma1<T>: Vec<T> = {
    T => vec![<>],
    <v:Comma1<T>> "," <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

Comment: () = {
    r"//.*" => ()
};
