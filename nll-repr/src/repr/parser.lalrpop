use intern::{self, InternedString};
use repr::*;
use std::str::FromStr;

grammar<'parse, 'arena>(arena: &'parse mut Arena<'arena>);

pub Func: Func<'arena> = {
    <blocks:BasicBlockData+> <assertions:Assertion+> => Func {
        data: blocks.into_iter()
                    .map(|block| (block.name, block))
                    .collect(),
        assertions: assertions,
    }
};

BasicBlockData: BasicBlockData<'arena> = {
    Comment* "block" <name:BasicBlock> "{"
        <actions:Action*>
        <gotos:("goto" <BasicBlock*> ";")?>
    "}" => {
        BasicBlockData {
            name: name,
            actions: actions,
            successors: gotos.unwrap_or(vec![]),
        }
    }
};

Assertion: Assertion<'arena> = {
    "assert" <a:Region> "=" <b:Region> ";" => {
        Assertion::RegionEq(a, b)
    },

    "assert" <p:RegionExit> "in" <b:Region> ";" => {
        Assertion::RegionContains(b, p)
    },
};

BasicBlock: BasicBlock = {
    Ident => BasicBlock(<>)
};

Action: Action<'arena> = {
    <a:Region> "<=" <b:Region> ";" => Action::Subregion(a, b),
    "*" <r:RegionVariable> ";" => Action::Deref(r),
};

Ident: InternedString = {
    r"[a-zA-Z_]\w*" => intern::intern(<>)
};

Region: Region<'arena> = {
    RegionData => arena.intern(<>),
};

RegionData: RegionData = {
    <v:RegionVariable> => RegionData::Variable(v),
    "{" <Comma<RegionExit>> "}" => RegionData::Exits(<>),
};

RegionVariable: RegionVariable = {
    "$" <name:Ident> => RegionVariable(name),
};

RegionExit: RegionExit = {
    <b:BasicBlock> "/" <a:r"[0-9]+"> =>
        RegionExit::Point(b, usize::from_str(a).unwrap()),
};

Comma<T>: Vec<T> = {
    <Comma1<T>> ","?
};

Comma1<T>: Vec<T> = {
    T => vec![<>],
    <v:Comma1<T>> "," <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

Comment: () = {
    r"//.*" => ()
};
